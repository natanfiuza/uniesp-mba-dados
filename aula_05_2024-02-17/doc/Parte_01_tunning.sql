CREATE DATABASE TUNNING;
GO

USE TUNNING;
GO


CREATE TABLE PESSOAS(
	ID INT NOT NULL IDENTITY(1,1),
	NOME VARCHAR(100) NOT NULL,
	SEXO CHAR(1) NOT NULL,
	SALARIO NUMERIC(7,2) NOT NULL,
	OBSERVACAO VARCHAR(4000) NULL,
	CPF CHAR(11) NOT NULL,
	CONSTRAINT PK_PESSOAS PRIMARY KEY CLUSTERED (ID),
	CONSTRAINT CHK_PESSOAS_SEXO CHECK(SEXO IN('M','F')),
	CONSTRAINT CHK_PESSOAS_SALARIO CHECK(SALARIO > 0),
	CONSTRAINT UNQ_PESSOAS_CPF UNIQUE(CPF)
);

DECLARE @CONTADOR SMALLINT, @QUANTIDADE SMALLINT
SELECT @CONTADOR = 1, @QUANTIDADE = 4000;

WHILE @CONTADOR <= @QUANTIDADE
BEGIN
	INSERT INTO PESSOAS
	(NOME, SEXO, SALARIO, OBSERVACAO, CPF)
	SELECT 
		'NOME' + CAST(@CONTADOR AS VARCHAR),
		CASE WHEN @CONTADOR%2 = 0 THEN 'M' ELSE 'F' END,
		((rand(checksum(newid()))*@CONTADOR)/3)*8,
		REPLICATE('A',@CONTADOR),
		CAST(@CONTADOR AS VARCHAR);

	SET @CONTADOR = @CONTADOR +1;
END;
GO

--EXPLICAR O ÍNDICE CLUSTER x HEAP
SELECT *
FROM PESSOAS;



--SELECIONAR E EXIBIR O PLANO DE EXECUÇÃO ESTIMADO / FALAR SOBRE OPERAÇÃO DE SCAN
SELECT *
FROM PESSOAS;

--SELECIONAR E EXIBIR O PLANO DE EXECUÇÃO ESTIMADO / FALAR SOBRE ÍNDICE NONCLUSTERED
SELECT CPF
FROM PESSOAS;


--SELECIONAR E EXIBIR O PLANO DE EXECUÇÃO ESTIMADO
SELECT CPF,ID
FROM PESSOAS;

--SELECIONAR E EXIBIR O PLANO DE EXECUÇÃO ESTIMADO
SELECT ID
FROM PESSOAS;

--SELECIONAR E EXIBIR O PLANO DE EXECUÇÃO ESTIMADO / FALAR SOBRE OPERAÇÃO DE SEEK
--(VARIAR O LIMITE FINAL COM OS VALORES: 1,2,10,100,200)
--TIPPING POINT

SELECT ID,CPF
FROM PESSOAS
WHERE ID BETWEEN 1 AND 100;

--SELECIONAR E EXIBIR O PLANO DE EXECUÇÃO ESTIMADO
SELECT ID,CPF,NOME
FROM PESSOAS
WHERE CPF BETWEEN 1 AND 3;


SELECT ID,CPF,NOME
FROM PESSOAS
WHERE CPF = 1;

--SELECIONAR E EXIBIR O PLANO DE EXECUÇÃO ESTIMADO
SELECT CPF
FROM PESSOAS
WHERE CPF * 2 > ID;


--SELECIONAR E EXIBIR O PLANO DE EXECUÇÃO ESTIMADO
--EVITAR "CÁLCULOS/OPERAÇÕES" NO LADO ESQUERDO DO WHERE
SELECT SALARIO
FROM PESSOAS
WHERE ID * 2 > 10;


SELECT SALARIO
FROM PESSOAS
WHERE ID > 5;


--SELECIONAR E EXIBIR O PLANO DE EXECUÇÃO ESTIMADO
SELECT ID,CPF
FROM PESSOAS
WHERE CPF BETWEEN 1 AND 3;

SELECT ID,CPF
FROM PESSOAS
WHERE CAST(CPF AS INT) BETWEEN 1 AND 3;


SELECT ID,CPF
FROM PESSOAS
WHERE CPF BETWEEN '1' AND '3';


--ANALISANDO A PERFORMANCE DE CONSULTAS
DBCC FREEPROCCACHE;
DBCC DROPCLEANBUFFERS;

SET STATISTICS TIME ON;
SET STATISTICS IO ON;


SELECT *
FROM PESSOAS;

--ATIVAR O PLANO DE EXECUÇÃO REAL E EXECUTAR A CONSULTA / VER A ABA DE MESSAGES E O PLANO DE EXECUÇÃO
SELECT CPF
FROM PESSOAS;



--TIPPING POINT
SELECT ID,CPF
FROM PESSOAS
WHERE ID BETWEEN 1 AND 10;

SELECT ID,CPF
FROM PESSOAS
WHERE ID BETWEEN 1 AND 100;

SELECT ID,CPF
FROM PESSOAS
WHERE ID BETWEEN 1 AND 105;

SELECT ID,CPF
FROM PESSOAS
WHERE ID BETWEEN 1 AND 106;


---------------

--CRIAR ÍNDICES
CREATE NONCLUSTERED INDEX IDX_PESSOAS_SEXO ON PESSOAS(SEXO);

--QUAL PLANO GERADO?
SELECT SEXO, ID
FROM PESSOAS
WHERE SEXO = 'M';

--E O PLANO AGORA? POSSO CONFIAR NA SUGESTÃO DO SQL SERVER PARA ÍNDICES?
SELECT SEXO, ID, NOME
FROM PESSOAS
WHERE SEXO = 'M';


CREATE NONCLUSTERED INDEX IDX_PESSOAS_SEXO2 ON PESSOAS(SEXO)
INCLUDE(NOME);

--COMENTAR PROCESSO DE ESCOLHA DE ÍNDICE PARA UMA CONSULTA
SELECT SEXO, ID, NOME
FROM PESSOAS
WHERE SEXO = 'M';


----------------------------

SELECT ID
FROM PESSOAS
WHERE CPF BETWEEN '1' AND '3000';


--POR QUE O PLANO DESSAS 2 CONSULTAS É DIFERENTE?
SELECT CPF
FROM PESSOAS
WHERE ID BETWEEN 1 AND 3000;

SELECT CPF
FROM PESSOAS
WHERE ID BETWEEN 1 AND 100;


------------------

CREATE INDEX IDX_PESSOAS_CPF_NOME ON PESSOAS(NOME,SEXO);


SELECT CPF
FROM PESSOAS
WHERE SALARIO > 1000;

--CREATE INDEX IDX_EXEMPLO1 ON PESSOAS(SALARIO,CPF);
CREATE INDEX IDX_EXEMPL02 ON PESSOAS(SALARIO) INCLUDE(CPF);



SELECT CPF
FROM PESSOAS
WHERE SALARIO > 1000;

------------

--COMENTAR O PROCESSO DE SEEK + LOOKUP / TIPPING POINT
SELECT NOME
FROM PESSOAS
WHERE SALARIO > 1000;