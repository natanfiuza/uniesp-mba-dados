USE TUNNING;
GO

CREATE TABLE FUNCIONARIOS(
	ID INT NOT NULL IDENTITY(1,1),
	NOME VARCHAR(100) NOT NULL,
	SEXO CHAR(1) NOT NULL,
	SALARIO NUMERIC(6,2) NOT NULL,
	DT_NASCIMENTO DATETIME NOT NULL,
	CEP CHAR(8) NOT NULL
);

DECLARE @CONTADOR SMALLINT, @QUANTIDADE SMALLINT
SELECT @CONTADOR = 1, @QUANTIDADE = 10000;

WHILE @CONTADOR <= @QUANTIDADE
BEGIN
	INSERT INTO FUNCIONARIOS
	(NOME,SEXO,SALARIO,DT_NASCIMENTO,CEP)
	SELECT
		'FUNCIONARIO' + CAST(@CONTADOR AS VARCHAR),
		CASE WHEN @CONTADOR%2 = 0 THEN 'F' ELSE 'M' END,
		(@CONTADOR%9)*1000,
		DATEADD(MONTH,-1*(1000%@CONTADOR),GETDATE()),
		'58' + CAST((@QUANTIDADE-@CONTADOR)+1 AS VARCHAR)

	SET @CONTADOR = @CONTADOR + 1;
END;

--ATIVAR O PLANO DE EXECUÇÃO REAL
SELECT * FROM FUNCIONARIOS;


ALTER TABLE FUNCIONARIOS 
ADD CONSTRAINT PK_FUNCIONARIOS
PRIMARY KEY CLUSTERED(ID);

SELECT * FROM FUNCIONARIOS;

------

ALTER TABLE FUNCIONARIOS DROP CONSTRAINT PK_FUNCIONARIOS;

ALTER TABLE FUNCIONARIOS ADD CONSTRAINT PK_FUNCIONARIOS
PRIMARY KEY NONCLUSTERED(ID);

CREATE CLUSTERED INDEX IDX_FUNCIONARIOS_CEP
ON FUNCIONARIOS(CEP);

SELECT NOME,CEP, SEXO
FROM FUNCIONARIOS
WHERE CEP BETWEEN '5812345' AND '5823456';

-----------

--E QUANDO TEMOS MAIS DE UM FILTRO, O QUE ACONTECE?
SELECT NOME, CEP, SEXO
FROM FUNCIONARIOS
WHERE SEXO = 'M'
AND DT_NASCIMENTO >= '19900101';

--QUAL COLUNA DEVE VIR PRIMEIRO NA CHAVE?!
CREATE INDEX IDX_EXEMPLO03 
ON FUNCIONARIOS(DT_NASCIMENTO,SEXO)
INCLUDE(NOME);

--VER O PLANO REAL (VER AS PROPRIEDADES DO OPERADOR DE CLUSTERED INDEX INSERT)
INSERT INTO FUNCIONARIOS
(NOME,SEXO,SALARIO,DT_NASCIMENTO,CEP)
VALUES
('FULANO','M',1000.00,CAST('19900101' AS DATETIME),'11111111');
GO

SELECT NOME, CEP, SEXO
FROM FUNCIONARIOS
WHERE SEXO = 'M'
AND DT_NASCIMENTO >= '19900101';
GO

DROP INDEX IDX_EXEMPLO03 ON FUNCIONARIOS;
GO

--E AGORA QUE INVERTEU, QUAL O IMPACTO?
CREATE INDEX IDX_EXEMPLO03 
ON FUNCIONARIOS(DT_NASCIMENTO)
INCLUDE(NOME,SEXO);
GO

SELECT NOME, CEP, SEXO
FROM FUNCIONARIOS
WHERE SEXO = 'M'
AND DT_NASCIMENTO >= '19900101';
GO

DROP INDEX IDX_EXEMPLO03 ON FUNCIONARIOS;
GO

CREATE INDEX IDX_EXEMPLO03 
ON FUNCIONARIOS(SEXO)
INCLUDE(NOME,DT_NASCIMENTO);
GO

SELECT NOME, CEP, SEXO
FROM FUNCIONARIOS
WHERE SEXO = 'M'
AND DT_NASCIMENTO >= '19900101';
GO

--O ÍNDICE FUNCIONA PARA <>? VER AS PROPRIEDADES DO OPERADOR (SEEK PREDICATES)
SELECT NOME, CEP, SEXO
FROM FUNCIONARIOS
WHERE SEXO <> 'M'
AND DT_NASCIMENTO >= '19900101';
GO

DROP INDEX IDX_EXEMPLO03 ON FUNCIONARIOS;
GO

CREATE INDEX IDX_EXEMPLO03 
ON FUNCIONARIOS(DT_NASCIMENTO,SEXO)
INCLUDE(NOME);
GO

CREATE INDEX IDX_EXEMPLO04
ON FUNCIONARIOS(SEXO,DT_NASCIMENTO)
INCLUDE(NOME);

--E QUANDO HÁ 2 ÍNDICES QUE ATENDEM A CONSULTA?
SELECT NOME, CEP, SEXO
FROM FUNCIONARIOS
WHERE SEXO = 'M'
AND DT_NASCIMENTO >= '19900101';

--SERÁ QUE ELE FILTRA PELA QUANTIDADE?
SELECT COUNT(*)
FROM FUNCIONARIOS
WHERE SEXO = 'M'

SELECT COUNT(*)
FROM FUNCIONARIOS
WHERE DT_NASCIMENTO >= '19900101';


--E SE EU MUDAR O OPERADOR

SELECT NOME, CEP, SEXO
FROM FUNCIONARIOS
WHERE SEXO < 'M'
AND DT_NASCIMENTO >= '19900101';

SELECT NOME, CEP, SEXO
FROM FUNCIONARIOS
WHERE SEXO = 'F'
AND DT_NASCIMENTO >= '19900101';




-------------
SELECT CEP,ID,NOME
FROM FUNCIONARIOS
WHERE SALARIO > 2000;

CREATE INDEX IDX_EXEMPLO05 ON FUNCIONARIOS(SALARIO);

SET STATISTICS IO ON

SELECT CEP,ID,NOME
FROM FUNCIONARIOS
WHERE SALARIO > 8000;

SELECT CEP,ID,NOME
FROM FUNCIONARIOS
WHERE SALARIO > CAST(8000 AS NUMERIC(6,2));

SELECT CEP,ID,NOME
FROM FUNCIONARIOS
WHERE SALARIO > 8000.00;

--ESSE ÍNDICE, ATENDERIA/COBRIRIA A CONSULTA?
CREATE INDEX IDX_EXEMPLO06 ON FUNCIONARIOS(SALARIO) INCLUDE(ID,NOME);

SELECT CEP,ID,NOME
FROM FUNCIONARIOS
WHERE SALARIO > 8000.00;

DROP INDEX IDX_EXEMPLO06 ON FUNCIONARIOS